
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьРеквизитыФормы();
	
	Для каждого _Элемент Из Элементы Цикл
		Если ТипЗнч(_Элемент) = Тип("ПолеФормы") Тогда
			Если Лев(_Элемент.ПутьКДанным, 2) = "сж" Тогда
				_Элемент.УстановитьДействие("ПриИзменении", "ТоварыПриИзменении");
			ИначеЕсли Лев(_Элемент.ПутьКДанным, 3) = "всд" Тогда
				_Элемент.УстановитьДействие("ПриИзменении", "ВСДПриИзменении");
			ИначеЕсли Лев(_Элемент.ПутьКДанным, 3) = "осм" Тогда
				_Элемент.УстановитьДействие("ПриИзменении", "РезультатыОсмотраПриИзменении");
			ИначеЕсли Лев(_Элемент.ПутьКДанным, 3) = "ттн" Тогда
				_Элемент.УстановитьДействие("ПриИзменении", "ТТНПриИзменении");
			ИначеЕсли Лев(_Элемент.ПутьКДанным, 3) = "упк" Тогда
				_Элемент.УстановитьДействие("ПриИзменении", "УпаковкаПриИзменении");
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ОтправительСвязь = Истина;
	
	ПолучательСвязь = Истина;
	
	МногоуровневаяУпаковка = Ветис.Настройки_МногоуровневаяУпаковка();
	
	ПоказатьСрокГодностиСтрокой = ЗначениеЗаполнено(сжСрокГодностиСтрокой);
	
	ПоказатьДатаВыработкиСтрокой = ЗначениеЗаполнено(сжДатаВыработкиСтрокой);
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ЗаполнитьРеквизитыФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьВидимостьКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если НЕ ПараметрыЗаписи.Свойство("РучнойВызов") Тогда
		ЗаписатьРеквизитыФормы();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	УстановитьВидимость("Запись");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если Параметр = ЭтаФорма.ИмяФормы Тогда
		//
	ИначеЕсли ИмяСобытия = "ВетисНастройки" Тогда
		УстановитьВидимость(ИмяСобытия);
	ИначеЕсли ИмяСобытия = "ВетисВерсия" Тогда
		УстановитьВидимость(ИмяСобытия);
	ИначеЕсли ИмяСобытия = "ВетисОтладка" Тогда
		//УстановитьВидимость(ИмяСобытия);
	ИначеЕсли ИмяСобытия = "ВетисТестовый" Тогда
		//
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура КомандаПровестиИЗакрыть(Команда)
	
	Если Ложь Тогда
		//
	ИначеЕсли ЗначениеЗаполнено(Объект.uuid) Тогда
		_текст = "Этот ВСД уже создан в сервисе";
		Если ВетисВызовСервера.РазрешенаОтладка() Тогда
			ПоказатьВопрос(Новый ОписаниеОповещения("ПередЗаписьюОбработкаОповещения", ЭтаФорма), _текст + "! Изменять его не рекомендуется. Продолжить?", РежимДиалогаВопрос.ДаНет);
		Иначе
			Сообщить(_текст + " изменять его нельзя!", СтатусСообщения.Внимание);
		КонецЕсли;
	Иначе
		ЗаписатьРеквизитыФормы();
		_Параматры = Новый Структура("РежимЗаписи, РучнойВызов", РежимЗаписиДокумента.Проведение);
		Записать(_Параматры);
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписьюОбработкаОповещения(пРезультат, пПараматры) Экспорт
	
	Если пРезультат = КодВозвратаДиалога.Да Тогда
		ЗаписатьРеквизитыФормы();
		_Параматры = Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение);
		Записать(_Параматры);
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаполнитьИзСервиса(Команда)
	
	Перем _Отказ, _Ошибка;
	
	КомандаЗаполнитьИзСервисаСервер(_Отказ, _Ошибка);
	
	Если _Отказ = Истина Тогда
		Сообщить(_Ошибка);
	КонецЕсли;;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПогасить(Команда)
	
	Перем _Отказ, _Ошибка;
	
	Если НЕ Объект.Проведен Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.Товары.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ (Ложь
		ИЛИ Объект.Статус = ПредопределенноеЗначение("Перечисление.ВетисVetDocumentStatus.ПустаяСсылка")
		ИЛИ Объект.Статус = ПредопределенноеЗначение("Перечисление.ВетисVetDocumentStatus.Оформлен")) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(сжПроизводительПредприятие) Тогда
		Сообщить("Не выбран производитель");
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(сжСтранаПроисхождения) Тогда
		Сообщить("Не выбрана страна происхождения");
		Возврат;
	КонецЕсли;
	
	//2017-12-26
	//перестал быть обязательным
	//Если НЕ ЗначениеЗаполнено(сжИДПартии) Тогда
	//	Сообщить("Не заполнен номер партии");
	//	Возврат;
	//КонецЕсли;
	
	Если Элементы.ГруппаРезультатыОсмотра.Видимость Тогда
		Если НЕ ЗначениеЗаполнено(осмНазначениеГруза) Тогда
			Сообщить("Не выбрано назначение груза");
			Возврат;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(осмБлагополучиеМестности) Тогда
			Сообщить("Не заполнено благополучие местности");
			Возврат;
		КонецЕсли;
	Иначе
		Если НЕ ЗначениеЗаполнено(всдНазначениеГруза) Тогда
			Сообщить("Не выбрано назначение груза");
			Возврат;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(всдБлагополучиеМестности) Тогда
			Сообщить("Не заполнено благополучие местности");
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	КомандаПогаситьСервер(_Отказ, _Ошибка);
	
	Если _Отказ = Истина Тогда
		Сообщить(_Ошибка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаВозврат(Команда)
	
	Перем _Отказ, _Ошибка;
	
	//сделать: тестировать возврат
	
	Если НЕ Объект.Проведен Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.Товары.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ (Ложь
		ИЛИ Объект.Статус = ПредопределенноеЗначение("Перечисление.ВетисVetDocumentStatus.ПустаяСсылка")
		ИЛИ Объект.Статус = ПредопределенноеЗначение("Перечисление.ВетисVetDocumentStatus.Оформлен")) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(сжПроизводительПредприятие) Тогда
		Сообщить("Не выбран производитель");
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(сжСтранаПроисхождения) Тогда
		Сообщить("Не выбрана страна происхождения");
		Возврат;
	КонецЕсли;
	
	Если Элементы.ГруппаРезультатыОсмотра.Видимость Тогда
		Если НЕ ЗначениеЗаполнено(осмНазначениеГруза) Тогда
			Сообщить("Не выбрано назначение груза");
			Возврат;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(осмБлагополучиеМестности) Тогда
			Сообщить("Не заполнено благополучие местности");
			Возврат;
		КонецЕсли;
	Иначе
		Если НЕ ЗначениеЗаполнено(всдНазначениеГруза) Тогда
			Сообщить("Не выбрано назначение груза");
			Возврат;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(всдБлагополучиеМестности) Тогда
			Сообщить("Не заполнено благополучие местности");
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	КомандаВозвратСервер(_Отказ, _Ошибка);
	
	Если _Отказ = Истина Тогда
		Сообщить(_Ошибка);
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура СопоставитьНажатие(Элемент)
	
	Если Элемент = Элементы.ОтправительСопоставить Тогда
		СопоставитьОткрытьФорму("ФормаКонтрагент", Объект.ОтправительGuid, "Объект.Отправитель");
	ИначеЕсли Элемент = Элементы.ПолучательСопоставить Тогда
		СопоставитьОткрытьФорму("ФормаКонтрагент", Объект.ПолучательGuid, "Объект.Получатель");
	ИначеЕсли Элемент = Элементы.ОтправительПредприятиеСопоставить Тогда
		СопоставитьОткрытьФорму("ФормаПредприятие", Объект.ОтправительПредприятиеGuid, "Объект.ОтправительПредприятие");
	ИначеЕсли Элемент = Элементы.ПолучательПредприятиеСопоставить Тогда
		СопоставитьОткрытьФорму("ФормаПредприятие", Объект.ПолучательПредприятиеGuid, "Объект.ПолучательПредприятие");
	ИначеЕсли Элемент = Элементы.НоменклатураСопоставить Тогда
		СопоставитьОткрытьФорму("ФормаНоменклатура1", сжProductItemGuid, "сжНоменклатура", Новый Структура("Предприятие", сжПроизводительПредприятие));
	ИначеЕсли Элемент = Элементы.НоменклатурнаяГруппаСопоставить Тогда
		СопоставитьОткрытьФорму("ФормаНоменклатурнаяГруппа", сжSubProductGuid, "сжНоменклатурнаяГруппа");
	ИначеЕсли Элемент = Элементы.СтранаПроисхожденияСопопставить Тогда
		СопоставитьОткрытьФорму("ФормаСтрана", сжСтранаПроисхожденияGuid, "сжСтранаПроисхождения");
	ИначеЕсли Элемент = Элементы.ПроизводительПредприятиеСопоставить Тогда
		СопоставитьОткрытьФорму("ФормаПредприятие", сжПроизводительПредприятиеGuid, "сжПроизводительПредприятие");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставитьОткрытьФорму(пИмяФормы, пGuid, пПутьКДанным, пПараметры = Неопределено)
	
	_Параметры = ?(пПараметры = Неопределено, Новый Структура, пПараметры);
	
	_Параметры.Вставить("guid", пGuid);
	
	_оо = Новый ОписаниеОповещения("СопоставитьНажатиеОбработкаОповещения", ЭтаФорма, Новый Структура("ПутьКДанным", пПутьКДанным));
	
	ОткрытьФорму("Обработка.ВетисУстановкаСоответствияСправочников.Форма."+пИмяФормы, _Параметры,,,,,_оо);
	
КонецПроцедуры

&НаКлиенте
Процедура СопоставитьНажатиеОбработкаОповещения(пРезультат, пПараметры) Экспорт
	
	Если НЕ пРезультат = Неопределено Тогда
		
		Выполнить(пПараметры.ПутьКДанным + " = пРезультат");
		
		Модифицированность = Истина;
		
		УстановитьВидимость("Сопоставление");
		
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
	ТоварыПриИзмененииНаСервере(Элемент.Имя);
	
	Если Элемент = Элементы.сжСкоропортящаяся Тогда
		УстановитьВидимость("Скоропортящаяся");
	КонецЕсли;
	////MERC13283 - При указании упаковки необходимо указать хотя бы одно из следующих полей - UUID/GUID/код.
	//Элементы.сжУпаковка.АвтоОтметкаНезаполненного = сжКоличествоМест > 0;
	
КонецПроцедуры

&НаСервере
Процедура ТоварыПриИзмененииНаСервере(ЭлементИмя)
	
	Если Элементы[ЭлементИмя].ПутьКДанным = "сжНоменклатура" Тогда
		сжЕдиницаИзмерения = сжНоменклатура.ЕдиницаХраненияОстатков;
		сжСтранаПроисхождения = сжНоменклатура.СтранаПроисхождения;
		сжНоменклатурнаяГруппа = сжНоменклатура.НоменклатурнаяГруппа;
	ИначеЕсли Элементы[ЭлементИмя].ПутьКДанным = "сжНоменклатурнаяГруппа" Тогда
		сжНоменклатура = Неопределено;
		сжЕдиницаИзмерения = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВСДПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатыОсмотраПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТТНПриИзменении(Элемент)
	
	Если Элемент = Элементы.ттнТранспорт Тогда
		ттнТранспортПриИзменении(Элемент);
	КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура УпаковкаПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры


&НаКлиенте
Процедура сжНоменклатураНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	//может быть вынести в настройку - "выбирать только сопоставленные"
	//ВетисФормы.ProductItemФормаВыбора(ЭтаФорма, "сжНоменклатураОбработкаОповещения", СтандартнаяОбработка, сжНоменклатура, сжПроизводительПредприятие);
	
КонецПроцедуры

&НаКлиенте
Процедура сжНоменклатураОбработкаОповещения(Результат, Параметр) Экспорт
	
	Если НЕ Результат = Неопределено Тогда
		Модифицированность = Истина;
		сжНоменклатура = Результат;
		сжProductItemName = "";
		сжProductItemGuid = "";
		сжSubProductName = "";
		сжSubProductGuid = "";
		ТоварыПриИзмененииНаСервере("сжНоменклатура");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура сжНоменклатурнаяГруппаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ВетисФормы.SubProductФормаВыбора(ЭтаФорма, "сжНоменклатурнаяГруппаОбработкаОповещения", СтандартнаяОбработка, сжНоменклатурнаяГруппа);
	
КонецПроцедуры

&НаКлиенте
Процедура сжНоменклатурнаяГруппаОбработкаОповещения(Результат, Параметр) Экспорт
	
	Если НЕ Результат = Неопределено Тогда
		Модифицированность = Истина;
		сжНоменклатурнаяГруппа = Результат;
		сжProductItemName = "";
		сжProductItemGuid = "";
		сжSubProductName = "";
		сжSubProductGuid = "";
		ТоварыПриИзмененииНаСервере("сжНоменклатурнаяГруппа");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура сжProductItemNameОчистка(Элемент, СтандартнаяОбработка)
	сжProductItemGuid = "";
КонецПроцедуры

&НаКлиенте
Процедура сжSubProductNameОчистка(Элемент, СтандартнаяОбработка)
	сжSubProductGuid = "";
КонецПроцедуры


&НаКлиенте
Процедура сжПроизводительПредприятиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ВетисФормы.EnterpriseФормаВыбора(ЭтаФорма, "сжПроизводительПредприятиеОбработкаОповещения", СтандартнаяОбработка, сжПроизводительПредприятие);
	
КонецПроцедуры

&НаКлиенте
Процедура сжПроизводительПредприятиеОбработкаОповещения(Результат, Параметр) Экспорт
	
	Если НЕ Результат = Неопределено Тогда
		Модифицированность = Истина;
		сжПроизводительПредприятие = Результат;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура сжСтранаПроисхожденияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ВетисФормы.CountryФормаВыбора(ЭтаФорма, "сжСтранаПроисхожденияОбработкаОповещения", СтандартнаяОбработка, сжСтранаПроисхождения);
	
КонецПроцедуры

&НаКлиенте
Процедура сжСтранаПроисхожденияОбработкаОповещения(Результат, Параметр) Экспорт
	
	Если НЕ Результат = Неопределено Тогда
		Модифицированность = Истина;
		сжСтранаПроисхождения = Результат;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ПолучательПриИзменении(Элемент)
	
	Объект.ПолучательПредприятие = Неопределено;
	Объект.ПолучательПредприятиеGuid = "";
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ВетисФормы.BusinessEntityФормаВыбора(ЭтаФорма, "ПолучательОбработкаОповещения", СтандартнаяОбработка, Объект.Получатель);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательОбработкаОповещения(Результат, Параметр) Экспорт
	
	Если НЕ Результат = Неопределено Тогда
		Модифицированность = Истина;
		Объект.Получатель = Результат;
		Объект.ПолучательПредприятие = Неопределено;
		Объект.ПолучательПредприятиеGuid = "";
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ПолучательПредприятиеПриИзменении(Элемент)
	
	Объект.ПолучательПредприятие = Неопределено;
	Объект.ПолучательПредприятиеGuid = "";
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательПредприятиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ПолучательСвязь Тогда
		Если ЗначениеЗаполнено(Объект.Получатель) Тогда
			ВетисФормы.EnterpriseФормаВыбора(ЭтаФорма, "ПолучательПредприятиеОбработкаОповещения", СтандартнаяОбработка, Объект.ПолучательПредприятие, Объект.Получатель);
		Иначе
			СтандартнаяОбработка = Ложь;
		КонецЕсли;
	Иначе
		ВетисФормы.EnterpriseФормаВыбора(ЭтаФорма, "ПолучательПредприятиеОбработкаОповещения", СтандартнаяОбработка, Объект.ПолучательПредприятие);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательПредприятиеОбработкаОповещения(Результат, Параметр) Экспорт
	
	Если НЕ Результат = Неопределено Тогда
		Модифицированность = Истина;
		Объект.ПолучательПредприятие = Результат;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ОтправительПриИзменении(Элемент)
	
	Объект.ОтправительПредприятие = Неопределено;
	Объект.ОтправительПредприятиеGuid = "";
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправительНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ВетисФормы.BusinessEntityФормаВыбора(ЭтаФорма, "ОтправительОбработкаОповещения", СтандартнаяОбработка, Объект.Отправитель);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправительОбработкаОповещения(пРезультат, пПараметры) Экспорт
	
	Если НЕ пРезультат = Неопределено Тогда
		Модифицированность = Истина;
		Объект.Отправитель = пРезультат;
		Объект.ОтправительПредприятие = Неопределено;
		Объект.ОтправительПредприятиеGuid = "";
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ОтправительПредприятиеПриИзменении(Элемент)
	
	Объект.ОтправительПредприятие = Неопределено;
	Объект.ОтправительПредприятиеGuid = "";
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправительПредприятиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ОтправительСвязь Тогда
		Если ЗначениеЗаполнено(Объект.Отправитель) Тогда
			ВетисФормы.EnterpriseФормаВыбора(ЭтаФорма, "ОтправительПредприятиеОбработкаОповещения", СтандартнаяОбработка, Объект.ОтправительПредприятие, Объект.Отправитель);
		Иначе
			СтандартнаяОбработка = Ложь;
		КонецЕсли;
	Иначе
		ВетисФормы.EnterpriseФормаВыбора(ЭтаФорма, "ОтправительПредприятиеОбработкаОповещения", СтандартнаяОбработка, Объект.ОтправительПредприятие);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправительПредприятиеОбработкаОповещения(Результат, Параметр) Экспорт
	
	Если НЕ Результат = Неопределено Тогда
		Модифицированность = Истина;
		Объект.ОтправительПредприятие = Результат;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ттнТранспортПриИзменении(Элемент)
	
	ттнТранспортПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Функция ттнТранспортПриИзмененииНаСервере()
	
	ттнНомерТранспорта = ттнТранспорт.Номер;
	
	ттнНомерПрицепа = ттнТранспорт.НомерПрицепа;
	
	ттнТипТранспорта = ттнТранспорт.ТипТранспорта;
	
	ттнПосредник = ттнТранспорт.Перевозчик;
	
	ттнСпособХранения = ттнТранспорт.СпособХранения;
	
КонецФункции

&НаКлиенте
Процедура ттнПосредникНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ВетисФормы.BusinessEntityФормаВыбора(ЭтаФорма, "ттнПосредникОбработкаОповещения", СтандартнаяОбработка, ттнПосредник);
	
КонецПроцедуры

&НаКлиенте
Процедура ттнПосредникОбработкаОповещения(Результат, Параметр) Экспорт
	
	Если НЕ Результат = Неопределено Тогда
		Модифицированность = Истина;
		ттнПосредник = Результат;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура УпаковкаПриАктивизацииСтроки(Элемент)
	
	_ТекущиеДанные = Элементы.Упаковка.ТекущиеДанные;
	
	Если _ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.Маркировка.ОтборСтрок = Новый ФиксированнаяСтруктура("КлючСвязи", _ТекущиеДанные.КлючСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура УпаковкаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.КлючСвязи = 1;//ссылка всегда на первую(единственную) строку Товары
		Элемент.ТекущиеДанные.КлючСтроки = ПолучитьКлючСтроки();
		Элементы.Маркировка.ОтборСтрок = Новый ФиксированнаяСтруктура("КлючСвязи", Элемент.ТекущиеДанные.КлючСтроки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УпаковкаПередУдалением(Элемент, Отказ)
	
	Для каждого _Строка Из Объект.Маркировка.НайтиСтроки(Новый Структура("КлючСвязи", Элементы.Упаковка.ТекущиеДанные.КлючСтроки)) Цикл
		Объект.Маркировка.Удалить(_Строка);
	КонецЦикла;
	
КонецПроцедуры


&НаКлиенте
Процедура МаркировкаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Элементы.Упаковка.ТекущиеДанные = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура МаркировкаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	_ТекущиеДанные = Элементы.Упаковка.ТекущиеДанные;
	
	Если _ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.КлючСвязи = _ТекущиеДанные.КлючСтроки;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ПоказатьСрокГодностиСтрокойПриИзменении(Элемент)
	
	УстановитьВидимость("СрокГодности");
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьДатаВыработкиСтрокойПриИзменении(Элемент)
	
	УстановитьВидимость("ДатаВыработки");
	
КонецПроцедуры


&НаКлиенте
Процедура ОтправительСвязьНажатие(Элемент)
	
	ОтправительСвязь = НЕ ОтправительСвязь;
	
	УстановитьВидимостьКлиент("ОтправительСвязь");
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательСвязьНажатие(Элемент)
	
	ПолучательСвязь = НЕ ПолучательСвязь;
	
	УстановитьВидимостьКлиент("ПолучательСвязь");
	
КонецПроцедуры


&НаСервере
Процедура ЗаполнитьРеквизитыФормы()
	
	Для каждого _Реквизит Из ПолучитьРеквизиты() Цикл
		Для каждого _Строка Из Объект.Товары Цикл
			Если Лев(_Реквизит.Имя, 2) = "сж" Тогда
				ЭтаФорма[_Реквизит.Имя] = _Строка[Сред(_Реквизит.Имя, 3)];
			КонецЕсли;
			Прервать;
		КонецЦикла;
		
		Для каждого _Строка Из Объект.ВСД Цикл
			Если Лев(_Реквизит.Имя, 3) = "всд" Тогда
				ЭтаФорма[_Реквизит.Имя] = _Строка[Сред(_Реквизит.Имя, 4)];
			КонецЕсли;
			Прервать;
		КонецЦикла;
		
		Для каждого _Строка Из Объект.РезультатыОсмотра Цикл
			Если Лев(_Реквизит.Имя, 3) = "осм" Тогда
				ЭтаФорма[_Реквизит.Имя] = _Строка[Сред(_Реквизит.Имя, 4)];
			КонецЕсли;
			Прервать;
		КонецЦикла;
		
		Для каждого _Строка Из Объект.ТТН Цикл
			Если Лев(_Реквизит.Имя, 3) = "ттн" Тогда
				ЭтаФорма[_Реквизит.Имя] = _Строка[Сред(_Реквизит.Имя, 4)];
			КонецЕсли;
			Прервать;
		КонецЦикла;
		
		Для каждого _Строка Из Объект.Упаковка Цикл
			Если Лев(_Реквизит.Имя, 3) = "упк" Тогда
				ЭтаФорма[_Реквизит.Имя] = _Строка[Сред(_Реквизит.Имя, 4)];
			КонецЕсли;
			Прервать;
		КонецЦикла;
		
		Для каждого _Строка Из Объект.Маркировка Цикл
			Если Лев(_Реквизит.Имя, 3) = "мрк" Тогда
				ЭтаФорма[_Реквизит.Имя] = _Строка[Сред(_Реквизит.Имя, 4)];
			КонецЕсли;
			Прервать;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьРеквизитыФормы()
	
	Объект.Товары.Очистить();
	Объект.ВСД.Очистить();
	Объект.ТТН.Очистить();
	Объект.РезультатыОсмотра.Очистить();
	
	Если НЕ МногоуровневаяУпаковка Тогда
		Объект.Упаковка.Очистить();
		Объект.Маркировка.Очистить();
	КонецЕсли;
	
	_СтрокаСЖ = Объект.Товары.Добавить();
	_СтрокаВСД = Объект.ВСД.Добавить();
	_СтрокаТТН = Объект.ТТН.Добавить();
	_СтрокаОСМ = Объект.РезультатыОсмотра.Добавить();
	
	_СтрокаСЖ.КлючСтроки = 1;
	
	Если НЕ МногоуровневаяУпаковка Тогда
		_СтрокаУПК = Объект.Упаковка.Добавить();
		_СтрокаМРК = Объект.Маркировка.Добавить();
		_СтрокаУПК.КлючСтроки = 1;
		_СтрокаУПК.КлючСвязи = 1;
		_СтрокаМРК.КлючСвязи = 1;
		упкУровень = ?(ЗначениеЗаполнено(упкУровень), упкУровень, Ветис.Настройки_УровеньУпаковки());
		мркКлассМаркировки = ?(ЗначениеЗаполнено(мркКлассМаркировки), мркКлассМаркировки, Ветис.Настройки_КлассМаркировки());
	КонецЕсли;
	
	Для каждого _Реквизит Из ПолучитьРеквизиты() Цикл
		Если Лев(_Реквизит.Имя, 2) = "сж" Тогда
			_СтрокаСЖ[Сред(_Реквизит.Имя, 3)] = ЭтаФорма[_Реквизит.Имя];
		ИначеЕсли Лев(_Реквизит.Имя, 3) = "всд" Тогда
			_СтрокаВСД[Сред(_Реквизит.Имя, 4)] = ЭтаФорма[_Реквизит.Имя];
		ИначеЕсли Лев(_Реквизит.Имя, 3) = "осм" Тогда
			_СтрокаОСМ[Сред(_Реквизит.Имя, 4)] = ЭтаФорма[_Реквизит.Имя];
		ИначеЕсли Лев(_Реквизит.Имя, 3) = "ттн" Тогда
			_СтрокаТТН[Сред(_Реквизит.Имя, 4)] = ЭтаФорма[_Реквизит.Имя];
		ИначеЕсли НЕ МногоуровневаяУпаковка И Лев(_Реквизит.Имя, 3) = "упк" Тогда
			_СтрокаУПК[Сред(_Реквизит.Имя, 4)] = ЭтаФорма[_Реквизит.Имя];
		ИначеЕсли НЕ МногоуровневаяУпаковка И Лев(_Реквизит.Имя, 3) = "мрк" Тогда
			_СтрокаМРК[Сред(_Реквизит.Имя, 4)] = ЭтаФорма[_Реквизит.Имя];
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры


&НаСервере
Процедура КомандаЗаполнитьИзСервисаСервер(пОтказ = Ложь, пОшибка = "")
	
	_vetDocument = ВетисMercuryApplicationsСлой1с.getVetDocumentByUuid(Объект, пОтказ, пОшибка);
	
	Если пОтказ = Истина Тогда
		Возврат;
	ИначеЕсли НЕ пОтказ = Истина Тогда
		Если Ветис.Версия_2_0() Тогда
			ВетисMercuryVetdocumentСлой1с.VetDocumentКонвертировать_20(Объект, _vetDocument);
		Иначе
			ВетисMercuryVetdocumentСлой1с.VetDocumentКонвертировать(Объект, _vetDocument);
		КонецЕсли;
		ЗаполнитьРеквизитыФормы();
		Если Ветис.РазрешенаОтладка() Тогда
			Модифицированность = Истина;
		ИначеЕсли ЗначениеЗаполнено(Объект.uuid) Тогда
			Записать();
		Иначе
			Модифицированность = Истина;
		КонецЕсли;
		УстановитьВидимость();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура КомандаПогаситьСервер(пОтказ = Ложь, пОшибка = "")
	
	_productItem = ?(ВетисВызовСервера.Версия_2_0(), ВетисArgusProductionСлой1с, ВетисDictionaryСлой1с).ProductItem(сжНоменклатура, сжПроизводительПредприятие);
	Если ЗначениеЗаполнено(_productItem.guid) Тогда
		//
	Иначе
		_subProduct  = ?(ВетисВызовСервера.Версия_2_0(), ВетисArgusProductionСлой1с, ВетисDictionaryСлой1с).SubProduct(сжНоменклатурнаяГруппа);
		Если НЕ ПустаяСтрока(_subProduct) Тогда
			//
		Иначе
			//не заполнены ключевые поля, выходим
			пОтказ = Истина;
			пОшибка = "Нет сопоставления для номенклатуры";
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ЗаписатьРеквизитыФормы();
	
	ВетисMercuryApplicationsСлой1с.IncomingOperation(Объект.Товары.Получить(0), Объект.Упаковка, Объект.Маркировка, Объект, Объект.Дата, пОтказ, пОшибка);
	
	Если НЕ пОтказ = Истина Тогда
		Записать();
		УстановитьВидимость();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура КомандаВозвратСервер(пОтказ = Ложь, пОшибка = "")
	
	_productItem = ?(ВетисВызовСервера.Версия_2_0(), ВетисArgusProductionСлой1с, ВетисDictionaryСлой1с).ProductItem(сжНоменклатура, сжПроизводительПредприятие);
	Если ЗначениеЗаполнено(_productItem.guid) Тогда
		//
	Иначе
		_subProduct  = ?(ВетисВызовСервера.Версия_2_0(), ВетисArgusProductionСлой1с, ВетисDictionaryСлой1с).SubProduct(сжНоменклатурнаяГруппа);
		Если НЕ ПустаяСтрока(_subProduct) Тогда
			//
		Иначе
			//не заполнены ключевые поля, выходим
			пОтказ = Истина;
			пОшибка = "Нет сопоставления для номенклатуры";
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ЗаписатьРеквизитыФормы();
	
	_сж = Объект.Товары.Получить(0);
	
	_Строка = Новый Структура("Возврат,ВозвратныйВСД,Количество,КоличествоМест,КоличествоВозврат,КоличествоМестВозврат", Истина,,
		_сж.Количество, _сж.КоличествоМест, _сж.Количество, _сж.КоличествоМест);
	
	ВетисMercuryApplicationsСлой1с.IncomingOperation(_Строка, Объект.Упаковка, Объект.Маркировка, Объект, Объект.Дата, пОтказ, пОшибка);
	
	Если НЕ пОтказ = Истина Тогда
		Записать();
		УстановитьВидимость();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьКлиент(пИмяСобытия = Неопределено)
	
	Если пИмяСобытия = "ОтправительСвязь" ИЛИ пИмяСобытия = Неопределено Тогда
		Элементы.ОтправительСвязь.Картинка = ?(ОтправительСвязь, БиблиотекаКартинок.ВетисЕстьСвязь, БиблиотекаКартинок.ВетисНетСвязи);
	КонецЕсли;
	
	Если пИмяСобытия = "ПолучательСвязь" ИЛИ пИмяСобытия = Неопределено Тогда
		Элементы.ПолучательСвязь.Картинка = ?(ПолучательСвязь, БиблиотекаКартинок.ВетисЕстьСвязь, БиблиотекаКартинок.ВетисНетСвязи);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимость(пИмяСобытия = Неопределено)
	
	_МногоуровневаяУпаковка = Ветис.Настройки_МногоуровневаяУпаковка();
	
	Если пИмяСобытия = Неопределено Тогда
		Элементы.uuid.Видимость = Ветис.Настройки_ПоказыватьGuid();
	КонецЕсли;
	
	Если пИмяСобытия = Неопределено ИЛИ пИмяСобытия = "ДатаВыработки" ИЛИ пИмяСобытия = "СрокГодности" Тогда
		Элементы.сжВыработкаНачало.Видимость = НЕ ПоказатьДатаВыработкиСтрокой;
		Элементы.сжВыработкаКонец.Видимость = НЕ ПоказатьДатаВыработкиСтрокой;
		Элементы.сжДатаВыработкиСтрокой.Видимость = ПоказатьДатаВыработкиСтрокой;
		Элементы.сжСрокГодностиНачало.Видимость = НЕ ПоказатьСрокГодностиСтрокой;
		Элементы.сжСрокГодностиКонец.Видимость = НЕ ПоказатьСрокГодностиСтрокой;
		Элементы.сжСрокГодностиСтрокой.Видимость = ПоказатьСрокГодностиСтрокой;
	КонецЕсли;
	
	Если пИмяСобытия = Неопределено ИЛИ пИмяСобытия = "ВетисНастройки" ИЛИ пИмяСобытия = "ВетисВерсия" Тогда
		_Версия20 = ?(ЗначениеЗаполнено(Объект.Версия), Объект.Версия, Ветис.Версия_2_0());
		Элементы.ГруппаУпаковка.Видимость = _Версия20 И _МногоуровневаяУпаковка;
		Элементы.ГруппаМаршрутСледования.Видимость = _Версия20 И Ветис.Настройки_МультимодальныеПеревозки();
		Элементы.ГруппаЛабораторныеИсследования.Видимость = _Версия20 И Ветис.Настройки_ЛабораторныеИсследования();
		Элементы.ГруппаМероприятия.Видимость = _Версия20 И Ветис.Настройки_Мероприятия();
		Элементы.ГруппаУсловияПеремещения.Видимость = _Версия20 И Ветис.Настройки_УсловияПеремещения();
		Элементы.ГруппаРезультатыОсмотра.Видимость = _Версия20;
		//Элементы.ГруппаПартии.Видимость = _Версия20;
		Элементы.ГруппаСвязанныеДокументы.Видимость = _Версия20 И ЗначениеЗаполнено(Объект.uuid);
		Элементы.ГруппаВСД.Видимость = НЕ _Версия20;
		Элементы.сжИДПартии.Видимость = _Версия20;
		Элементы.ГруппаПродукцияКолонка1Упаковка.Видимость = НЕ Элементы.ГруппаУпаковка.Видимость;
		Элементы.ГруппаПродукцияКолонка2Упаковка.Видимость = НЕ Элементы.ГруппаУпаковка.Видимость;
	КонецЕсли;
	
	Если пИмяСобытия = "ВетисНастройки" И НЕ МногоуровневаяУпаковка = _МногоуровневаяУпаковка Тогда
		Если МногоуровневаяУпаковка Тогда
			ЗаполнитьРеквизитыФормы();
		Иначе
			ЗаписатьРеквизитыФормы();
		КонецЕсли;
	КонецЕсли;
	
	Если пИмяСобытия = "ВетисОтладка" ИЛИ пИмяСобытия = Неопределено Тогда
		_Отладка = Ветис.РазрешенаОтладка();
		Элементы.ТипПоле1.Видимость = Ложь;
		//Элементы.ТипПоле1.Видимость = _Отладка;
		//Элементы.Тип1.Видимость = НЕ _Отладка;
		Элементы.ГруппаДополнительно.Видимость = _Отладка;
		Элементы.ГруппаStatusChange.Видимость = _Отладка;
		Элементы.ГруппаОтветственный.Видимость = _Отладка;
	КонецЕсли;
	
	Если пИмяСобытия = "Запись" ИЛИ пИмяСобытия = Неопределено Тогда
		Элементы.ФормаЗаполнитьИзСервиса.Видимость = ЗначениеЗаполнено(Объект.uuid);
		Элементы.ФормаКомандаПогасить.Видимость = Объект.Проведен И (Ложь
			ИЛИ Объект.Статус = ПредопределенноеЗначение("Перечисление.ВетисVetDocumentStatus.ПустаяСсылка")
			ИЛИ Объект.Статус = ПредопределенноеЗначение("Перечисление.ВетисVetDocumentStatus.Оформлен"));
		Элементы.ФормаКомандаВозврат.Видимость = Элементы.ФормаКомандаПогасить.Видимость;
	КонецЕсли;
	
	Если пИмяСобытия = "Сопоставление" ИЛИ пИмяСобытия = Неопределено Тогда
		Элементы.Отправитель.Видимость = ЗначениеЗаполнено(Объект.Отправитель) ИЛИ НЕ ЗначениеЗаполнено(Объект.ОтправительGuid);
		Элементы.ОтправительСопоставить.Видимость = НЕ Элементы.Отправитель.Видимость;
		
		Элементы.ОтправительПредприятие.Видимость = ЗначениеЗаполнено(Объект.ОтправительПредприятие) ИЛИ НЕ ЗначениеЗаполнено(Объект.ОтправительПредприятиеGuid);
		Элементы.ОтправительПредприятиеСопоставить.Видимость = НЕ Элементы.ОтправительПредприятие.Видимость;
		
		Элементы.Получатель.Видимость = ЗначениеЗаполнено(Объект.Получатель) ИЛИ НЕ ЗначениеЗаполнено(Объект.ПолучательGuid);
		Элементы.ПолучательСопоставить.Видимость = НЕ Элементы.Получатель.Видимость;
		
		Элементы.ПолучательПредприятие.Видимость = ЗначениеЗаполнено(Объект.ПолучательПредприятие) ИЛИ НЕ ЗначениеЗаполнено(Объект.ПолучательПредприятиеGuid);
		Элементы.ПолучательПредприятиеСопоставить.Видимость = НЕ Элементы.ПолучательПредприятие.Видимость;
		
		Элементы.сжНоменклатура.Видимость = ЗначениеЗаполнено(сжНоменклатура) ИЛИ НЕ ЗначениеЗаполнено(сжProductItemGuid);
		Элементы.НоменклатураСопоставить.Видимость = НЕ Элементы.сжНоменклатура.Видимость;
		
		Элементы.сжНоменклатурнаяГруппа.Видимость = ЗначениеЗаполнено(сжНоменклатурнаяГруппа) ИЛИ НЕ ЗначениеЗаполнено(сжSubProductGuid);
		Элементы.НоменклатурнаяГруппаСопоставить.Видимость = НЕ Элементы.сжНоменклатурнаяГруппа.Видимость;
		
		Элементы.сжСтранаПроисхождения.Видимость = ЗначениеЗаполнено(сжСтранаПроисхождения) ИЛИ НЕ ЗначениеЗаполнено(сжСтранаПроисхожденияGuid);
		Элементы.СтранаПроисхожденияСопопставить.Видимость = НЕ Элементы.сжСтранаПроисхождения.Видимость;
		
		Элементы.сжПроизводительПредприятие.Видимость = ЗначениеЗаполнено(сжПроизводительПредприятие) ИЛИ НЕ ЗначениеЗаполнено(сжПроизводительПредприятиеGuid);
		Элементы.ПроизводительПредприятиеСопоставить.Видимость = НЕ Элементы.сжПроизводительПредприятие.Видимость;
	КонецЕсли;
	
	Если пИмяСобытия = "Скоропортящаяся" ИЛИ пИмяСобытия = Неопределено Тогда
		Элементы.сжВыработкаНачало.ФорматРедактирования = ?(сжСкоропортящаяся, "ДФ='dd.MM.yyyy hh'", "ДЛФ=D");
		Элементы.сжВыработкаКонец.ФорматРедактирования = ?(сжСкоропортящаяся, "ДФ='dd.MM.yyyy hh'", "ДЛФ=D");
		Элементы.сжСрокГодностиНачало.ФорматРедактирования = ?(сжСкоропортящаяся, "ДФ='dd.MM.yyyy hh'", "ДЛФ=D");
		Элементы.сжСрокГодностиКонец.ФорматРедактирования = ?(сжСкоропортящаяся, "ДФ='dd.MM.yyyy hh'", "ДЛФ=D");
	КонецЕсли;
	
	МногоуровневаяУпаковка = _МногоуровневаяУпаковка;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьКлючСтроки()
	
	_список = Объект.Упаковка.Выгрузить().ВыгрузитьКолонку("КлючСтроки");
	
	Для _Индекс = 1 По 999 Цикл
		Если _список.Найти(_Индекс) = Неопределено Тогда
			Возврат _Индекс;
		КонецЕсли;
	КонецЦикла;
	
КонецФункции
